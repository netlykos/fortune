plugins {
  id "org.netlykos.fortune.npm-build-conventions"
  id "java"
  id "distribution"
}

ext {
  distDir       = "dist/"
  webAppDir     =  "ng-web-app"
  webAppDistDir = "${distDir}/${webAppDir}"
}

jar {
  from "${webAppDistDir}" into "static"
}

task buildWebApp(type: YarnTask) {
  dependsOn yarn_install
  // run the npm serve task
  args = ['run', 'build']

  inputs.dir("main/src")
  outputs.dir("${distDir}")
}

task serveWebApp(type: YarnTask) {
  // run the npm serve task
  args = ['run', 'start']
}

clean.doLast {
  delete "${distDir}"
}

distributions {
  main {
    contents {
      into "/"
      fileMode 0644
      from ("${webAppDistDir}") {
        filter(org.apache.tools.ant.filters.FixCrLfFilter)
        include "**/*.html", "**/*.js", "**/*.css", "**/*.map", "**/*.json"
      }
      from ("${webAppDistDir}") {
        exclude "**/*.html", "**/*.js", "**/*.css", "**/*.map", "**/*.json"
      }
      from(tasks.writeManifestProperties.propertiesFile)
    }
  }
}

distTar {
  compression = Compression.GZIP
  archiveExtension = "tar.gz"
}

[distZip, distTar, jar]*.dependsOn buildWebApp, writeManifestProperties
